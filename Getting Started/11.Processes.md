# プロセス

 Elixirでは、全てのコードはプロセスの中で動く。お互いのプロセスは切り離されており、それぞれ並行して動き、プロセス間はmessage passingの仕組みを使って通信する。プロセスはElixirにおける並列処理の基礎になるだけでなく、分散型で障害に強いプログラムを作る手段を与えてくれる。

 ElixirのプロセスとOSで言う所のプロセスを混同してはいけない。Elixirのプロセスはメモリ消費量、CPU消費量の観点から見てとてつもなく軽量だ(他のプログラム言語のスレッドとは違って)。だから、同時に10個のプロセスを持つことはもちろん、数百数千のプロセスを動かすことは珍しい事ではない。

 ここでは、プロセスを生成するための基本的な仕組みや、プロセス間のmessage passingについて学んでいく。

## `spawn`

 プロセスを生成する基本的な方法は、自動的にimportされている`spawn/1`関数を使うことだ。

```elixir
iex> spawn fn -> 1 + 2 end
#PID<0.43.0>
```

 `spawn/1`は、新しいプロセスで実行させる関数を引数に取る。

 `spawn/1`はPID(プロセス識別子)を返すことに注意。この時点で、生成されたプロセスはおそらく死んでいる。プロセスは与えられた関数を実行し、完了するとexitする。

```elixir
iex> pid = spawn fn -> 1 + 2 end
#PID<0.44.0>
iex> Process.alive?(pid)
false
```

 現在のプロセスのPIDを取得するには、`self/0`を呼び出す。

```elixir
iex> self()
#PID<0.41.0>
iex> Process.alive?(self())
true
```

 メッセージの受信、送信が出来るようになれば、プロセスはもっと興味深いものになる。

## `send` と `receive`

 `send/2`を使ってメッセージの送信、`receive/1`でメッセージの受信が出来る。

```elixir
iex> send self(), {:hello, "world"}
{:hello, "world"}
iex> receive do
...>   {:hello, msg} -> msg
...>   {:world, msg} -> "won't match"
...> end
"world"
```

 メッセージがプロセスに`send`されると、メッセージはプロセスの「メールボックス」に送られる。`receive/1`で、パターンに合致するメッセージがメールボックスに無いか、探しに行く。`receive/1`はガード文や、`case/2`等の多くの句をサポートしている。

 `send/2`ではプロセスの実行をブロックしない。受信プロセスのメールボックスにメッセージを突っ込むだけだ。自分自身に送信する時にも同じ。

 パターンにマッチするメッセージがメールボックスになかった場合、プロセスはマッチするメッセージが到着するまで待機する。その際のタイムアウトを指定する事もできる。

```elixir
iex> receive do
...>   {:hello, msg}  -> msg
...> after
...>   1_000 -> "nothing after 1s"
...> end
"nothing after 1s"
```

 メールボックスに常にメッセージがある事を期待するなら、タイムアウトに0秒を指定する事もできる。

 それでは、違うプロセスにメッセージを送ってみよう。

```elixir
iex> parent = self()
#PID<0.41.0>
iex> spawn fn -> send(parent, {:hello, self()}) end
#PID<0.48.0>
iex> receive do
...>   {:hello, pid} -> "Got hello from #{inspect pid}"
...> end
"Got hello from #PID<0.48.0>"
```

 `inspect/1`は、内部的なデータ構造を文字列に変換する関数だ。`receive`ブロックが実行される頃には、生成したプロセスが終了している事に注意しよう。そのプロセスの役目はメッセージを送信する事だけだ。

 iexを使っているなら、`flush/0`を使うと便利だ。こいつはメールボックスのメッセージを全てひっくり返して出力する。

```elixir
iex> send self(), :hello
:hello
iex> flush()
:hello
:ok
```

## リンク

 Elixirでプロセスを生成する時には、大抵`linked`プロセスとして生成する。`spawn_link/1`のサンプルを紹介する前に、`spawn/1`が失敗した時に何が起こるか見てみよう。

```elixir
iex> spawn fn -> raise "oops" end
#PID<0.58.0>

[error] Process #PID<0.58.00> raised an exception
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
```

 単にエラーがログに出てきただけで、親プロセスは動き続けている。これはプロセスが独立しているからだ。エラーを他のプロセスに伝播させたい場合は、プロセスをリンクさせなければならない。`spawn_link/1`で実現できる。

```elixir
iex> self()
#PID<0.41.0>
iex> spawn_link fn -> raise "oops" end

** (EXIT from #PID<0.41.0>) evaluator process exited with reason: an exception was raised:
    ** (RuntimeError) oops
        (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6

[error] Process #PID<0.289.0> raised an exception
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
```

 プロセスがリンクされているため、別のプロセスからEXITシグナルを受け取り終了していることが読み取れる。

 `Process.link/1`を呼び出してもリンクできる。[`Process`モジュールについてのドキュメント](https://hexdocs.pm/elixir/Process.html)を一読する事をおススメする。

 プロセスとそのリンクは、耐障害性の高いシステムを作り上げるのに重要な役割を果たす。Elixirのプロセスは、デフォルトでは独立しており、何も共有していない。なので、プロセスの中でのエラーは、他のプロセスに影響を及ぼさない。一方で、プロセス間のリンクにより、エラーを伝播させるような繋がりを確立する事が出来る。よくあるのは、プロセスのエラーを検知し、その後新たなプロセスを立ち上げるようなスーパーバイザにリンクする手法だ。

 他のプログラム言語では、例外をキャッチし、適切にハンドリングしなければならない。しかしElixirでは、スーパーバイザがシステムを適切に再起動させることを期待できるため、エラーのあったプロセスをそのまま落とすことが出来る。「Failing fast」はElixirの基本哲学だ。

 実のところ、`spawn/1`と`spawn_link/1`はプロセスを生成するための基本的なプリミティブだ。大抵の場合はこれらのプリミティブを抽象化したものを使うことになる。もっとも基本的な例がタスクだ。

## タスク

 タスクはより良いエラーリポートと内部状態の情報を与えてくれる。

```elixir
iex(1)> Task.start fn -> raise "oops" end
{:ok, #PID<0.55.0>}

15:22:33.046 [error] Task #PID<0.55.0> started from #PID<0.53.0> terminating
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
    (elixir) lib/task/supervised.ex:85: Task.Supervised.do_apply/2
    (stdlib) proc_lib.erl:247: :proc_lib.init_p_do_apply/3
Function: #Function<20.99386804/0 in :erl_eval.expr/5>
    Args: []
```

 `spawn/1`や`spawn_link/2`の代わりに、`Task.start/1`と`Task.start_link/1`を使っている。これらはPIDだけではなく、`{:ok, pid}`の形で値を返す。これにより、タスクをsupervision treeで使うことが出来る。さらに、`Task`は`Task.async/1`や`Task.await/1`のように、簡単に分散システムを作るための関数を提供してくれる。

 まぁ、詳しくは __Mix and OTP guide__ で見ていくことにしよう。今は`Task`はより良いエラーリポートを得るために使う、と覚えていれば十分だ。

## ステート

 これまでステート(state, 状態)についてあまり話してこなかった。状態が必要なアプリケーションを作りたい場合、例えばコンフィグを保持したり、ファイルを解析した結果をメモリに持っておきたかったりする場合、どこに保存すればいいのだろうか？

 プロセスがもっとも普遍的なアンサーだ。無限ループし状態を管理し、メッセージの送受信を行うプロセスだ。例えば、`kv.exs`というファイルに、key-valueストアとして動作するプロセスを生成するモジュールを書いてみよう。

```elixir
defmodule KV do
  def start_link do
    Task.start_link(fn -> loop(%{}) end)
  end

  defp loop(map) do
    receive do
      {:get, key, caller} ->
        send caller, Map.get(map, key)
        loop(map)
      {:put, key, value} ->
        loop(Map.put(map, key, value))
    end
  end
end
```

 この中の`start_link`は、空のマップを引数にして`loop/1`を実行するプロセスを生成している。`loop/1`はメッセージを待ち受け、それによって適切なアクションをとる関数だ。`:get`メッセージを受信した場合、呼び出し元プロセスにメッセージを投げ返し、再び新たなメッセージを待つため`loop/1`を呼び出す。`:put`の場合は、マップに`key`と`value`を保管し、そのマップを引数にして`loop/1`を呼び出す。

 `iex kv.exs`を動かしてみよう。

```elixir
iex> {:ok, pid} = KV.start_link
{:ok, #PID<0.62.0>}
iex> send pid, {:get, :hello, self()}
{:get, :hello, #PID<0.41.0>}
iex> flush()
nil
:ok
```

 まず、マップには何もキーが無いため、`:get`を送信してメールボックスをflushしてみても、`nil`が入っているだけだ。`:put`を実行して再度トライしてみよう。

```elixir
iex> send pid, {:put, :hello, :world}
{:put, :hello, :world}
iex> send pid, {:get, :hello, self()}
{:get, :hello, #PID<0.41.0>}
iex> flush()
:world
:ok
```

 これが、プロセスが状態を保持する方法と、メッセージの送信によって状態を取得したり更新したりする方法だ。実際、`pid`を知っているプロセス全てに対して、メッセージを送信し状態を操作する事が出来る。

 `pid`に名前を付け、その名前を知っているプロセス全てがメッセージを送信出来るようにする事も可能だ。

```elixir
iex> Process.register(pid, :kv)
true
iex> send :kv, {:get, :hello, self()}
{:get, :hello, #PID<0.41.0>}
iex> flush()
:world
:ok
```

 状態管理にプロセスを使ったり、プロセスに名前を付けたりすることは良く使われるパターンだ。しかし、実際には上記のようなパターンを自分たちで実装することは無く、Elixirが用意する様々な抽象化されたメソッド等を使うことになる。

  例えば、状態周りを抽象化した[agents](https://hexdocs.pm/elixir/Agent.html)がある。

```elixir
iex> {:ok, pid} = Agent.start_link(fn -> %{} end)
{:ok, #PID<0.72.0>}
iex> Agent.update(pid, fn map -> Map.put(map, :hello, :world) end)
:ok
iex> Agent.get(pid, fn map -> Map.get(map, :hello) end)
:world
```

 `Agent.start_link/2`に`:name`オプションを渡すことができ、その場合名前が自動的に登録される。`Agent`の他に、Elixirは一般的なサーバ(`GenServer`と呼ばれる)やタスク等を作るためのAPIを提供している。これらについて、詳しくは __Mix and OTP guide__ で見ていこう。

 それじゃ、次はElixirでの入出力について学ぶとしよう。