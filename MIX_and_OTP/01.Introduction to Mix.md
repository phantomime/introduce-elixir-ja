# Mixの導入

 このガイドでは、supervision tree、設定、テスト等を学ぶと共に、Elixirアプリケーションをビルドする方法についても学ぶ。

 サンプルとして、分散型のkey-value storeアプリケーションを作る事を考えよう。僕たちはkey-value pairをバケツの中に入れることとし、また、そのバケツは複数のノードに分散して配置する。また、それぞれのノードにアクセスできるようなシンプルなクライアントも作る事にしよう。クライアントは以下のように動作する。

```test
CREATE shopping
OK

PUT shopping milk 1
OK

PUT shopping eggs 3
OK

GET shopping milk
1
OK

DELETE shopping eggs
OK
```

 このアプリケーションを作るためには、以下の3つのツールの力が必要だ。

- __OTP__(Open Telecom Platform)
  - こいつはアプリケーションをErlang上で動かすためのライブラリの集合だ。Erlangを使う開発者は、頑強で障害に強いアプリケーションをビルドするためにOTPを使う。ここでは、Elixirに統合されたOTPの様々な側面、特にsupervision treesやevent manager等について見ていこう。
- __Mix__
  - こいつはアプリケーションをElixir上で動かすために、プロジェクトの作成、コンパイル、テスト、依存管理等を行ってくれるビルドツールだ。
- __ExUnix__
  - こいつはElixir上で動く単体テストフレームワークだ。

この章では、Mixを使ってプロジェクトを作成した後、OTP、Mix、UxUnitの機能について見ていこう。

## プロジェクトの作成

 君がElixirをインストールした時、`elixir`や`elixirc`、`iex`といった実行ファイルの他にも、`mix`という名前のElixirスクリプトを手に入れているはずだ。

 コマンドラインで`mix new`を入力してプロジェクトを生成しよう。このコマンドでは、プロジェクト名を引数として渡す(ここでのプロジェクト名は`kv`だ)。また、メインモジュールの名前は`KV`にしてほしい、という事も伝えよう。デフォルトだと`Kv`となってしまう。

```bash
$ mix new kv --module KV
* creating README.md
* creating .formatter.exs
* creating .gitignore
* creating mix.exs
* creating config
* creating config/config.exs
* creating lib
* creating lib/kb.ex
* creating test
* creating test/test_helper.exs
* creating test/kb_test.exs
```

 Mixは`kv`という名前のディレクトリを生成する。その中にはいくつかのファイルが配置されている。

 それぞれのファイルを簡単に見ていこう。

## プロジェクトのコンパイル

`mix.exs`という名前のファイルは、プロジェクトの設定を行うためのものだ。ちょっと見てみよう。

```elixir
defmodule KV.MixProject do
  use Mix.Project

  def project do
    [
      app: :kv,
      version: "0.1.0",
      elixir: "~> 1.6-dev",
      start_permanent: Mix.env == :prod,
      deps: deps()
    ]
  end

  # Run "mix help compile.app" to learn about applications.
  def application do
    [
      extra_applications: [:logger]
    ]
  end

  # Run "mix help deps" to learn about dependencies.
  defp deps do
    [
      # {:dep_from_hexpm, "~> 0.3.0"},
      # {:dep_from_git, git: "https://github.com/elixir-lang/my_dep.git", tag: "0.1.0"},
    ]
  end
end
```

 `mix.exs`は2つの関数を定義している。`project`はプロジェクトの名前やバージョンを返すものだ。`application`はアプリケーションを生成するために使われる。

 `deps`という名前のプライベート関数もある。これは`project`から呼び出され、プロジェクトの依存関係を定義するものだ。`deps`を分割した関数として定義する事は推奨されてはいないが、プロジェクト設定の整理整頓には役立つだろう。

 Mixは`lib/kb.ex`というファイルも生成している。ここには、`hello`という関数が定義されたモジュールが存在する。

```elixir
defmodule KV do
  @moduledoc """
  Documentation for KV.
  """

  @doc """
  Hello world.

  ## Examples

      iex> KV.hello()
      :world

  """
  def hello do
    :world
  end
end
```

 これだけで、プロジェクトをコンパイルするには十分だ。

```bash
$ cd kv
$ mix compile
Compiling 1 file (.ex)
Generated kv app
```

 `lib/kv.ex`がコンパイルされ`kv.app`が生成され、以前説明したように、全てのプロトコルは1つに統合される。コンパイルの成果物は、`mix.exs`ファイルのオプションに従って、全て`_build`ディレクトリの中に配置される。

 プロジェクトをコンパイルすると、以下のようにして、プロジェクト内で`iex`セッションを開始出来る。

```bash
$ iex -S mix
```

## テストの実行

 Mixはプロジェクトのテストを実行するための適切な仕組みも生成してくれる。Mixのプロジェクトでは、`lib`ディレクトリにあるそれぞれのファイルに対して、`test`ディレクトリ内で`<filename>_test.exs`という名前のファイルを作る、という慣習がある。そして、僕たちは既に、`lib/kv.ex`に対して、`test/kv_test.exs`をもっている。この時点ではあまり大したものでは無い。

```elixir
defmodule KVTest do
  use ExUnit.Case
  doctest KV

  test "greets the world" do
    assert KV.hello() == :world
  end
end
```

重要な点は二つある。

1. テストファイルは`exs`ファイルである。わざわざコンパイルする必要が無くて便利だからだ。
2. `KVTest`というモジュールを定義し、テスト用のAPIを使うために`ExUnit.Case`を使用し、`test/2`を使ってシンプルなテストを定義している。

 Mixは`test/test_helper.exs`というファイルも生成している。これはテストフレームワークをセットアップするためのものだ。

```elixir
ExUnit.start()
```

このファイルはテストを実行する度に必要になる。`mix test`を実行する事でテストを実行出来る。

```bash
$ mix test
Compiling 1 file (.ex)
Generated kv app
..

Finished in 0.05 seconds
1 doctest, 1 test, 0 failures

Randomized with seed 926414
```

 `mix test`を実行する事で、Mixはソースファイルを再度コンパイルする事に注意。これはMixが複数の __environment__ をサポートしている事によるものだ。これについては次章で見ていく。

 また、ExUnitは成功したテストにつきドットを出力し、さらに自動的に乱数の生成を行う。それでは、わざとテストを失敗させ、何が起こるか見てみよう。

 `test/kv_test.exs`のアサーションを以下のように変えてみる。

```elixir
assert KV.hello() == :oops
```

 `mix test`をもう一度実行してみよう(ここではコンパイルは走らない)。

```elixir
$ mix test
.

  1) test greets the world (KVTest)
     test/kv_test.exs:5
     Assertion with == failed
     code:  assert KV.hello() == :oops
     left:  :world
     right: :oops
     stacktrace:
       test/kv_test.exs:6: (test)



Finished in 0.06 seconds
1 doctest, 1 test, 1 failure

Randomized with seed 898870
```

 失敗したテストについて、ExUnitはテスト名、テストの内容、`==`演算子の左側と右側の値の差異のような詳細なレポートを出力する。

 2行目のテスト名の横には、テストが定義されている行番号が記されている。この行全体をコピーして`mix test`に渡すことで、Mixはその特定のテストを実行する。

```bash
$ mix test test/kv_test.exs:5
```

 特定のテストを繰り返し実行したいときに死ぬほど便利だ。

 最後のスタックトレースは、そのテストがどこで失敗したのかのヒントになる。

## 自動コードフォーマット

 `mix.new`で生成されるファイルに`.formatter.exs`がある。Elixirはコードフォーマッターを走らせ、僕たちのコードを一貫性のあるスタイルに自動的にフォーマットする。フォーマッターは`mix format`で実行される。`.formatter.exs`はどのファイルをフォーマットすべきか指定するのに使うわけだ。

 フォーマッターを試すために、`lib`や`test`ディレクトリのファイルに、余分なスペースや行を追加して`mix format`を実行してみよう。

 ほとんどのエディタは、セーブした時やキーボードショートカットを契機にしてファイルをフォーマットするような機能を備えている。Elixirを学ぶなら、そのようなエディタの機能が、Elixirの文法についてのフィードバックを与えてくれるだろう。

 開発チームには、CIサーバの中で`mix format --check-formatted`を実行する事をおススメする。現在、そして将来のコードがフォーマットされている事を保障してくれる。

 より詳しくは「[the format task documentation](https://hexdocs.pm/mix/Mix.Tasks.Format.html)」をチェックする事。

## Environment

 Mixは「environment(環境)」という概念をサポートしている。これにより、開発者は種々のシナリオによってコンパイルをカスタマイズしたり、様々なオプションを設定できたりする。デフォルトでは、Mixは以下の3つのenvironmentを解釈する。

- `:dev`
  -  デフォルトで、`compile`のようなMixのタスクに実行されるもの
- `:test`
  - `mix test`で使われるもの
- `:prod`
  - プロジェクトを製品化する時に使うもの

 environmentは現在のプロジェクトにのみ適用される。後で見るように、追加された依存関係はデフォルトでは`:prod`environmentで実行される。

 evironmentごとのカスタマイズは`mix.exs`ファイル中の`Mix.env`関数にアクセスする事によって行われる。この関数は現在のenvironmentをatomとして返す。`start_permanent`オプションで使っていた物がまさにそれだ。

```elixir
def project do
  [...,
   start_permanent: Mix.env == :prod,
   ...]
end
```

 これがtrueであれば、`start_permanent`オプションはアプリケーションをpermanentモードで開始する。これは、もしアプリケーションのsupervision treeがシャットダウンした時に、Erlang VMがクラッシュするものだ。トラブルシューティングの目的ではVMインスタンスは起動したままなのが望ましいので、`dev`や`test` environmentでは好ましくない動きだ。

 Mixは、`test`タスクを実行するために`:test` environmentを使う以外は、デフォルトで`:dev` environmentを用いる。environmentは、環境変数の`MIX_ENV`に書き込むことで変更できる。

```bash
$ MIX_ENV=prod mix compile
```

